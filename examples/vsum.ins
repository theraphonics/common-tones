(defun bit20 (x)			;modifier got 2's complement 20 bit interpreted as fraction 
  (if (>= x (expt 2 19))
      (float (/ (- x (expt 2 20)) (expt 2 19)))
    (float (/ x (expt 2 19)))))

(defun make-frobber-function (beg end frobl)
  (let ((result (list beg))
	(val (bit20 (cadr frobl))))
    (loop for x in frobl by #'cddr and 
              y in (cdr frobl) by #'cddr do
      (when (and (>= x beg)
		 (<= x end))
	(push val result)
	(push x result)
	(setf val (bit20 y))))
    (push val result)
    (push end result)
    (push val result)
    (nreverse result)))

(defvar vsum-fm-index 1.0)
(defvar vsum-amp-env '(0 0  25 1  75 1  100 0))
(defvar vsum-periodic-vibrato-rate 5.0)
(defvar vsum-random-vibrato-rate 16.0)
(defvar vsum-periodic-vibrato-amplitude 0.0025)
(defvar vsum-random-vibrato-amplitude 0.005)
(defvar vsum-noise-amount 0.0)
(defvar vsum-noise-freq 1000.0)
(defvar vsum-gliss-env '(0 0  100 0))
(defvar vsum-glissando-amount 0.0)
(defvar vsum-fm1-env '(0 1  25 .4  75 .6  100 0))
(defvar vsum-fm2-env vsum-fm1-env)
(defvar vsum-fm3-env vsum-fm1-env)
(defvar vsum-fm1-rat 2.0)
(defvar vsum-fm2-rat 6.0)
(defvar vsum-fm3-rat 8.0)
(defvar vsum-base nil)
(defvar vsum-frobber nil)
(defvar vsum-reverb-amount 0.01)
(defvar vsum-index-type :violin)
(defvar vsum-degree 0.0)
(defvar vsum-distance 1.0)
(defvar vsum-denoise nil)
(defvar vsum-index1 nil)
(defvar vsum-index2 nil)
(defvar vsum-index3 nil)

(defun restore-vsum-defaults ()
  (setf vsum-fm-index 1.0)
  (setf vsum-amp-env '(0 0  25 1  75 1  100 0))
  (setf vsum-periodic-vibrato-rate 5.0)
  (setf vsum-random-vibrato-rate 16.0)
  (setf vsum-periodic-vibrato-amplitude 0.0025)
  (setf vsum-random-vibrato-amplitude 0.005)
  (setf vsum-noise-amount 0.0)
  (setf vsum-noise-freq 1000.0)
  (setf vsum-gliss-env '(0 0  100 0))
  (setf vsum-glissando-amount 0.0)
  (setf vsum-fm1-env '(0 1  25 .4  75 .6  100 0))
  (setf vsum-fm2-env vsum-fm1-env)
  (setf vsum-fm3-env vsum-fm1-env)
  (setf vsum-fm1-rat 2.0)
  (setf vsum-fm2-rat 6.0)
  (setf vsum-fm3-rat 8.0)
  (setf vsum-base nil)
  (setf vsum-frobber nil)
  (setf vsum-reverb-amount 0.01)
  (setf vsum-index-type :violin)
  (setf vsum-denoise nil)
  (setf vsum-index1 nil)
  (setf vsum-index2 nil)
  (setf vsum-index3 nil)
  (setf vsum-degree 0.0)
  (setf vsum-distance 1.0))

(definstrument vsum
	       (startime dur frequency amplitude
		&key (fm-index                   vsum-fm-index)
		     (amp-env                    vsum-amp-env)
		     (periodic-vibrato-rate      vsum-periodic-vibrato-rate)
		     (random-vibrato-rate        vsum-random-vibrato-rate)
		     (periodic-vibrato-amplitude vsum-periodic-vibrato-amplitude)
		     (random-vibrato-amplitude   vsum-random-vibrato-amplitude)
		     (noise-amount               vsum-noise-amount)
		     (noise-freq                 vsum-noise-freq)
		     (gliss-env                  vsum-gliss-env)
		     (glissando-amount           vsum-glissando-amount)
		     (fm1-env                    vsum-fm1-env)
		     (fm2-env                    vsum-fm2-env)
		     (fm3-env                    vsum-fm3-env)
		     (fm1-rat                    vsum-fm1-rat)
		     (fm2-rat                    vsum-fm2-rat)
		     (fm3-rat                    vsum-fm3-rat)
		     (fm1-index                  vsum-index1)
		     (fm2-index                  vsum-index2)
		     (fm3-index                  vsum-index3)
		     (base                       vsum-base)
		     (frobber                    vsum-frobber)
		     (reverb-amount              vsum-reverb-amount)
		     (index-type                 vsum-index-type)
		     (degree                     vsum-degree)
		     (distance                   vsum-distance)
		     (denoise                    vsum-denoise)
		&allow-other-keys)
  (if (> (abs amplitude) 1.0) (error "amplitude = ~A?" amplitude))
  (if (< (abs frequency) 1.0) (error "frequency = ~A?" frequency))
  (let* ((beg (floor (* startime *srate*)))
	 (end (+ beg (floor (* dur *srate*))))
	 
	 (frq-scl (hz->radians frequency))
	 (fm2-ratio (if (> frequency (/ *srate* 12.0)) 2.0 6.0))
	 (fm3-ratio (if (> frequency (/ *srate* 16.0)) 2.0 8.0))
	 (modulate (not (zerop fm-index)))
	 (maxdev (* frq-scl fm-index))
	 (vln (not (eq index-type :cello)))
	 (easy-case (and (zerop noise-amount)
			 (eq fm1-env fm2-env)
			 (eq fm1-env fm3-env)
			 (zerop (- fm1-rat (floor fm1-rat)))
			 (zerop (- fm2-ratio (floor fm2-ratio)))
			 (zerop (- fm3-ratio (floor fm3-ratio)))))
	 ;; if the envelopes are all the same, and each ratio is an integer, we can use
	 ;; waveshaping to get the modulation very cheaply -- i.e. around 10 instructions 
	 ;; as opposed to 200 instructions used by 3 sine calcs, 3 envelopes,
	 ;; and 6 double-precision multiplies. (And it is extremely cheap to add more 
	 ;; moduators here because each is just a multiply-add at run time!)

	 (ifrequency (* frequency .5))
	 (index1 (or fm1-index (min pi (* maxdev (/ (if vln 5.0 7.5) (log ifrequency))))))
	 (index2 (or fm2-index (min pi (* maxdev 3.0 (if vln 
							     (/ (- 8.5 (log ifrequency)) (+ 3.0 (/ ifrequency 1000)))
							   (/ 15.0 (sqrt ifrequency)))))))
	 (index3 (or fm3-index (min pi (* maxdev (/ (if vln 4.0 8.0) (sqrt ifrequency))))))

	 (coeffs (and easy-case modulate
		      (partials->polynomial
		       (list fm1-rat index1
			     fm2-ratio index2
			     fm3-ratio index3))))
	 (norm (or (and easy-case modulate 1.0) index1))
	 
	 (carrier (make-oscil :frequency frequency))
	 (carrier2 (make-oscil :frequency (* 2 frequency)))
	 (carrier3 (if vln (make-oscil :frequency (* 3 frequency))))
	 (fmosc1  (and modulate (make-oscil :frequency (* fm1-rat frequency))))
	 (fmosc2  (and modulate (or easy-case (make-oscil :frequency (* fm2-ratio frequency)))))
	 (fmosc3  (and modulate (or easy-case (make-oscil :frequency (* fm3-ratio frequency)))))
	 (ampf  (make-env :envelope (if denoise 
					(reduce-amplitude-quantization-noise amp-env dur amplitude)
				      amp-env)
			  :base base :duration dur
			  :scaler (if vln (/ amplitude 3) (* amplitude .5))))
	 (indf1 (and modulate
		     (make-env :envelope fm1-env  :duration dur
			       :scaler norm)))
	 (indf2 (and modulate
		     (or easy-case
			 (make-env :envelope fm2-env  :duration dur
				   :scaler index2))))
	 (indf3 (and modulate
		     (or easy-case 
			 (make-env :envelope fm3-env  :duration dur
				   :scaler index3))))
	 (frqf (make-env :envelope gliss-env :duration dur
			 :scaler (* glissando-amount frq-scl)))
	 (pervib (make-triangle-wave :frequency periodic-vibrato-rate 
				     :amplitude (* periodic-vibrato-amplitude frq-scl)))
	 (ranvib (make-rand-interp :frequency random-vibrato-rate 
			     :amplitude (* random-vibrato-amplitude frq-scl)))
	 (fm-noi (if (and (/= 0.0 noise-amount)
			  (null frobber))
		     (make-rand :frequency noise-freq
				 :amplitude (* pi noise-amount))))
	 (frb-env (if (and (/= 0.0 noise-amount)
			   frobber)
		      (make-env :envelope (make-frobber-function startime (+ startime dur) frobber)
				:base 0 :duration dur
				:scaler (* two-pi noise-amount))))
	 (vib 0.0) 
	 (modulation 0.0)
	 (loc (make-locsig :degree degree :reverb reverb-amount :distance distance))
	 (fuzz 0.0))
    (run
     (loop for i from beg to end do
       (if (/= 0.0 noise-amount)
	   (if (null frobber)
	       (setf fuzz (rand fm-noi))
	     (setf fuzz (env frb-env))))
       (setf vib (+ (env frqf) (triangle-wave pervib) (rand-interp ranvib)))
       (if modulate
	   (if easy-case
	       (setf modulation
		 (* (env indf1) 
		    (polynomial coeffs (oscil fmosc1 vib))))
	     (setf modulation
	       (+ (* (env indf1) (oscil fmosc1 (+ (* fm1-rat vib) fuzz)))
		  (* (env indf2) (oscil fmosc2 (+ (* fm2-ratio vib) fuzz)))
		  (* (env indf3) (oscil fmosc3 (+ (* fm3-ratio vib) fuzz)))))))
       (locsig loc i
	     (* (env ampf)
		(+ (oscil carrier (+ vib modulation))
		   (oscil carrier2 (+ vib modulation))
		   (if carrier3 (oscil carrier3 (+ vib modulation)) 0.0))))))))


;;; the "fuzz" term is not scaled to fit each modulator (this is the way the SAM version worked)


